{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c382828c-b065-441a-982a-3cb363b40aff",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "import Pkg; Pkg.activate(\".\")\n",
    "using FRESCO\n",
    "import FUSE, QED, IMAS, VacuumFields\n",
    "using Plots, ProgressMeter"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "884e13b9-c3d5-44b2-a43f-5e470c95baa2",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "recycle = true\n",
    "if recycle\n",
    "    dd = IMAS.json2imas(\"./dd_iter.json\")\n",
    "else\n",
    "    dd, ini, act = FUSE.init(:ITER, init_from=:ods);\n",
    "    FUSE.ActorEquilibrium(dd, act; ip_from=:pulse_schedule);\n",
    "    IMAS.imas2json(dd, \"./dd_iter.json\");\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2706f945-d85e-4903-aa9c-2eccf34df63a",
   "metadata": {},
   "outputs": [],
   "source": [
    "canvas0 = FRESCO.Canvas(dd, 65);\n",
    "profile0 = FRESCO.PprimeFFprime(dd);\n",
    "eqt = dd.equilibrium.time_slice[];\n",
    "cp1d = dd.core_profiles.profiles_1d[];\n",
    "Q0 = FUSE.qed_init_from_imas(eqt, cp1d; uniform_rho = 101);\n",
    "η0 = FUSE.η_imas(cp1d);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "112bb282-371c-4a43-a0c6-20d0346bdcae",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Solve equilibrium with shape control to get self-consistent coil currents\n",
    "canvas, profile = deepcopy(canvas0), deepcopy(profile0);\n",
    "control = :shape;\n",
    "# Keep currents fixed in ITER OH coils (1:6), VS coils(13:14), and vacuum vessel(15:162)\n",
    "fixed_coils = vcat(1:6, 13:14, 15:162);\n",
    "@time FRESCO.solve!(canvas, profile, 100, 3; control, relax=0.25, tolerance=1e-12, debug=true, initialize_current=true, fixed_coils);\n",
    "plot(canvas, size=(500,300))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3dded78d-cb20-434d-bcfa-9d2e664e26d6",
   "metadata": {},
   "outputs": [],
   "source": [
    "# SAVE AT THIS POINT\n",
    "canvas_save = deepcopy(canvas);\n",
    "dd_save = deepcopy(dd);\n",
    "Q_save = deepcopy(Q0);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f829aac4-c057-48df-ae6b-56fc6a33fa1f",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Perturb the VS3U coil current by 1 kA (starts at 0)\n",
    "canvas1, profile1 = deepcopy(canvas), deepcopy(profile);\n",
    "dI_13 = 1e3;\n",
    "VacuumFields.set_current!(canvas1.coils[13], dI_13);\n",
    "@time FRESCO.solve!(canvas1, profile1, 100, 3; relax=0.25, tolerance=1e-6, debug=true, control=:eddy, initialize_current=false, initialize_mutuals=true);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dd62537a-0d61-4df8-8324-65738ce84ae6",
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"Shift = $(round(canvas1.Zaxis - canvas.Zaxis, digits=5)) m\")\n",
    "p1 = plot(canvas, xrange=(1,13), yrange=(-8.5,8.5), title=\"Initial\", clims=(-160,160))\n",
    "p2 = plot(canvas1, xrange=(1,13), yrange=(-8.5,8.5), title=\"Shifted with Eddys\", clims=(-160,160), ylabel=\"\")\n",
    "plot(p1,p2, size=(800,450),left_margin=4Plots.mm)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b521ffae-1655-4f38-b690-e778a2e646e1",
   "metadata": {},
   "outputs": [],
   "source": [
    "# This creates a QED Build struct based on a FRESCO canvas.coils\n",
    "# The voltage waveforms are set to be constant values that match V=IR\n",
    "function Build(canvas, dd)\n",
    "\n",
    "    coils = canvas.coils\n",
    "    Mcc = [VacuumFields.mutual(c1, c2) for c1 in coils, c2 in coils]\n",
    "    Ic = [VacuumFields.current(c) / VacuumFields.turns(c) for c in coils] #current per turn\n",
    "    Rc = [VacuumFields.resistance(c) for c in coils];\n",
    "    Vc = Ic .* Rc\n",
    "    V_waveforms = [QED.Waveform(Vc[k]) for k in eachindex(Vc)]\n",
    "    \n",
    "    eqt = dd.equilibrium.time_slice[]\n",
    "    cp1d = dd.core_profiles.profiles_1d[]\n",
    "    Ip = eqt.global_quantities.ip\n",
    "    \n",
    "    # COIL MUTUALS\n",
    "    #image = VacuumFields.Image(eqt)\n",
    "    #Mpc = [VacuumFields.mutual(image, coil, Ip) for coil in coils]\n",
    "    Mpc = [-FRESCO.plasma_flux_at_coil(k, canvas) / Ip for k in eachindex(coils)]\n",
    "    dMpc_dt = zero(Mpc) # How Mpc changes in time (like shape)... to test later\n",
    "    \n",
    "    # INTERNAL INDUCTANCE\n",
    "    It = IMAS.cumtrapz(cp1d.grid.area, cp1d.j_tor)\n",
    "    Wp = 0.5 * IMAS.trapz(cp1d.grid.psi, It)\n",
    "    Li = 2 * Wp / Ip^2\n",
    "    \n",
    "    # EXTERNAL INDUCTANCE\n",
    "    ψb = eqt.profiles_1d.psi[end]\n",
    "    ψc = sum(Mpc[k] * Ic[k] for k in eachindex(coils))\n",
    "    Le = (ψb - ψc) / Ip\n",
    "    Lp = Li + Le\n",
    "\n",
    "    Pohm = dd.core_sources.source[:ohmic].profiles_1d[].electrons.power_inside[end]\n",
    "    Ini = dd.core_profiles.global_quantities.current_non_inductive[]\n",
    "    Iohm = Ip - Ini\n",
    "    Rp = Pohm / (Ip * Iohm)\n",
    "\n",
    "    Vni = Rp * Ini\n",
    "    \n",
    "    return QED.QED_build(Ic, Vc, Rc, Mcc, Vni, Rp, Lp, Mpc, dMpc_dt, V_waveforms);\n",
    "end\n",
    "\n",
    "function update_build!(build, canvas, dd)\n",
    "    coils = canvas.coils\n",
    "\n",
    "    eqt = dd.equilibrium.time_slice[]\n",
    "    cp1d = dd.core_profiles.profiles_1d[]\n",
    "    Ip = eqt.global_quantities.ip\n",
    "    #image = VacuumFields.Image(eqt)    \n",
    "    \n",
    "    for (k, coil) in enumerate(coils)\n",
    "        build.Ic[k] =  VacuumFields.current(coil) / VacuumFields.turns(coil)\n",
    "        #build.Mpc[k] = VacuumFields.mutual(image, coil, Ip)\n",
    "        build.Mpc[k] = FRESCO.plasma_flux_at_coil(k, canvas) / Ip\n",
    "    end\n",
    "    \n",
    "    # INTERNAL INDUCTANCE\n",
    "    It = IMAS.cumtrapz(cp1d.grid.area, cp1d.j_tor)\n",
    "    Wp = 0.5 * IMAS.trapz(cp1d.grid.psi, It)\n",
    "    Li = 2 * Wp / Ip^2\n",
    "    \n",
    "    # EXTERNAL INDUCTANCE\n",
    "    ψb = eqt.profiles_1d.psi[end]\n",
    "    ψc = sum(build.Mpc[k] * build.Ic[k] for k in eachindex(coils))\n",
    "    Le = (ψb - ψc) / Ip\n",
    "    build.Lp = Li + Le\n",
    "\n",
    "    Pohm = dd.core_sources.source[:ohmic].profiles_1d[].electrons.power_inside[end]\n",
    "    Ini = dd.core_profiles.global_quantities.current_non_inductive[]\n",
    "    Iohm = Ip - Ini\n",
    "    build.Rp = Pohm / (Ip * Iohm)\n",
    "\n",
    "    build.Vni = build.Rp * Ini\n",
    "    \n",
    "    return build\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7e09e4cd-4eae-41a6-aca0-153b74a6f560",
   "metadata": {},
   "outputs": [],
   "source": [
    "function update_from_fresco!(dd, canvas, profile)\n",
    "    eq = dd.equilibrium\n",
    "    eqt = eq.time_slice[]\n",
    "    eqt1d = eqt.profiles_1d\n",
    "    eq2d = resize!(eqt.profiles_2d, 1)[1]\n",
    "\n",
    "    eqt.global_quantities.magnetic_axis.r = canvas.Raxis\n",
    "    eqt.global_quantities.magnetic_axis.z = canvas.Zaxis\n",
    "    eqt.global_quantities.psi_boundary = canvas.Ψbnd\n",
    "    eqt.global_quantities.psi_axis = canvas.Ψaxis\n",
    "\n",
    "    Npsi = length(eqt1d.psi)\n",
    "    eqt1d.psi = range(canvas.Ψaxis, canvas.Ψbnd, Npsi)\n",
    "    # p' doesn't change\n",
    "    eqt1d.f_df_dpsi = profile.ffprime.(eqt1d.psi_norm) .* profile.ffp_scale\n",
    "\n",
    "    fend = eq.vacuum_toroidal_field.b0[end] * eq.vacuum_toroidal_field.r0\n",
    "    f2 = 2 * IMAS.cumtrapz(eqt1d.psi, eqt1d.f_df_dpsi)\n",
    "    f2 .= f2 .- f2[end] .+ fend ^ 2\n",
    "    eqt1d.f = sign(fend) .* sqrt.(f2)\n",
    "\n",
    "    pend = eqt1d.pressure[end]\n",
    "    eqt1d.pressure = IMAS.cumtrapz(eqt1d.psi, eqt1d.dpressure_dpsi)\n",
    "    eqt1d.pressure .+= pend .- eqt1d.pressure[end]\n",
    "\n",
    "    eq2d.grid_type.index = 1\n",
    "    eq2d.grid.dim1 = collect(range(canvas.Rs[1], canvas.Rs[end], Npsi))\n",
    "    eq2d.grid.dim2 = collect(range(canvas.Zs[1], canvas.Zs[end], Npsi))\n",
    "    FRESCO.update_interpolation!(canvas)\n",
    "    eq2d.psi = [canvas._Ψitp(r, z) for r in eq2d.grid.dim1, z in eq2d.grid.dim2]\n",
    "\n",
    "    fw = IMAS.first_wall(dd.wall)\n",
    "    IMAS.flux_surfaces(eqt, fw.r, fw.z)\n",
    "\n",
    "    return dd\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bc07204e-95b2-4957-b892-953dbaf44388",
   "metadata": {},
   "outputs": [],
   "source": [
    "function VDE!(dd::IMAS.dd, canvas::FRESCO.Canvas, profile::FRESCO.CurrentProfile, \n",
    "              QI::QED.QED_state, build::QED.QED_build, Δt::Real, Nt::Int;\n",
    "              tolerance::Real=1e-6, relax::Real=0.25, Nout::Int=200, Nin::Int=3,\n",
    "              converge_hard::Bool=false)\n",
    "\n",
    "    ps = Plots.Plot{Plots.GRBackend}[]\n",
    "    Zs = zeros(Nt+1)\n",
    "    Zs[1] = canvas.Zaxis\n",
    "    eqt = dd.equilibrium.time_slice[]\n",
    "    A0 = eqt.global_quantities.area\n",
    "    pp0 = profile.pprime\n",
    "    @showprogress for k in 1:Nt\n",
    "    \n",
    "        # Update canvas from QED structs\n",
    "        canvas.Ip = QED.Ip(QI)\n",
    "        for (k, coil) in enumerate(canvas.coils)\n",
    "            VacuumFields.set_current!(coil, build.Ic[k] * VacuumFields.turns(coil))\n",
    "        end\n",
    "\n",
    "        #profile.pprime = x -> (2.0 * k / Nt) * pp0(x)\n",
    "\n",
    "        # Perform GS solve\n",
    "        k>1 && FRESCO.set_flux_at_coils!(canvas)\n",
    "        success = true\n",
    "        try\n",
    "            status = FRESCO.solve!(canvas, profile, Nout, Nin; relax, tolerance, debug=false, control=:eddy, initialize_current=false, initialize_mutuals=(k==1))\n",
    "            if converge_hard && (status != 0)\n",
    "                success = false\n",
    "            else\n",
    "                update_from_fresco!(dd, canvas, profile)\n",
    "            end\n",
    "            if eqt.global_quantities.area / A0 < 0.1\n",
    "                # plasma is too small. exit\n",
    "                success = false\n",
    "            end\n",
    "        catch e\n",
    "            # Something happened... often plasma got too small \n",
    "            display(e)\n",
    "            success = false\n",
    "        end\n",
    "        \n",
    "        if !success\n",
    "            Zs[k+1:end] .= NaN\n",
    "            break\n",
    "        end\n",
    "      \n",
    "        Zs[k+1] = canvas.Zaxis\n",
    "        push!(ps, plot(canvas, xrange=(0.75,2.75), yrange=(-2,2), colorbar=false))\n",
    "    \n",
    "        # Update QED structs\n",
    "        QI = FUSE.qed_init_from_imas(dd.equilibrium.time_slice[], dd.core_profiles.profiles_1d[]; uniform_rho = 101)\n",
    "        update_build!(build, canvas, dd)\n",
    "        \n",
    "        # Evolve coupled system\n",
    "        QI = QED.evolve(QI, η0, build, Δt, 1000)\n",
    "    end\n",
    "\n",
    "    return ps, Zs\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "581f167d-3255-4911-a30a-96df380d4c42",
   "metadata": {},
   "outputs": [],
   "source": [
    "Δt = 2e-3;\n",
    "Nt = 200;\n",
    "relax = 0.25;\n",
    "tolerance = 1e-6;\n",
    "Nout = 200;\n",
    "Nin = 3;\n",
    "\n",
    "dd = deepcopy(dd_save);\n",
    "canvas = deepcopy(canvas_save);\n",
    "VacuumFields.set_current!(canvas.coils[13], 1e3); # set upper VS coil current to 1 kA to perturb\n",
    "QI = deepcopy(Q_save);\n",
    "build = Build(canvas, dd);\n",
    "ps, Zs = VDE!(dd, canvas, profile, QI, build, Δt, Nt; relax, tolerance, Nout, Nin, converge_hard=false);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e252f98e-4ca0-4245-862d-b4b2691b40a7",
   "metadata": {},
   "outputs": [],
   "source": [
    "for (k,p) in enumerate(ps)\n",
    "    plot!(p, title=\"t=$(round(1e3k*Δt)) ms\", xlabel=\"\", ylabel=\"\", link=:both, xrange=(1,13),yrange=(-8,8))\n",
    "end\n",
    "k = length(ps)\n",
    "plot!(ps[end], title=\"Final t=$(round(1e3k*Δt)) ms\", xlabel=\"\", ylabel=\"\", link=:both, xrange=(1,13),yrange=(-8,8))\n",
    "\n",
    "dN = Nt ÷ 10\n",
    "if length(ps) == Nt\n",
    "    display(plot(ps[dN:dN:end]..., layout=(2,5), size=(1300,700)))\n",
    "else\n",
    "    display(plot(vcat(ps[dN:dN:end], ps[end])..., layout=(2,5), size=(1300,700)))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "de7564b1-44a0-42a5-9227-8ff463fbe9be",
   "metadata": {},
   "outputs": [],
   "source": [
    "ts = range(0.0, Nt * Δt * 1000., Nt+1)\n",
    "Zs[end-20:end] .= NaN\n",
    "pz = plot(ts, Zs .- Zs[1], xlabel=\"t (ms)\", ylabel=\"ΔZ (m)\", lw=2, legend=nothing)\n",
    "display(pz)\n",
    "if true\n",
    "    plot!(pz, yscale=:log10, ylims=(1e-2,3))\n",
    "    gamma = 1 / 507\n",
    "    fac = 4.2\n",
    "    @show 1.0 / (fac * gamma)\n",
    "    display(plot!(ts, exp.(-3.3 .+ fac.* gamma .* ts)))\n",
    "end\n",
    "#plot!(pz, ts, Zs65 .- Zs65[1], lw=2)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.6",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
